load("//bazel:skia_app_container.bzl", "skia_app_container")

genrule(
    name = "gen_version",
    outs = ["VERSION.txt"],
    cmd = "grep STABLE_GIT_REVISION bazel-out/stable-status.txt | cut -d ' ' -f 2 > $@",
    stamp = 1,
)

filegroup(
    name = "configs",
    srcs = glob(["configs/*.json"]),
    visibility = ["//perf:__subpackages__"],
)

filegroup(
    name = "configs_spanner",
    srcs = glob(["configs/spanner/*.json"]),
    visibility = ["//perf:__subpackages__"],
)

skia_app_container(
    name = "perfserver",
    base_image = "@base-cipd//image",
    dirs = {
        "/usr/local/bin": [
            [
                "//perf/go/perfserver:perfserver",
                "0755",
            ],
            [
                "//perf/go/perf-tool:perf-tool",
                "0755",
            ],
        ],
        "/usr/local/share/skiaperf/configs": [
            [
                ":configs",
                "0644",
            ],
        ],
        "/usr/local/share/skiaperf/configs/spanner": [
            [
                ":configs_spanner",
                "0644",
            ],
        ],
        "/usr/local/share/skiaperf/dist/images": [
            [
                "//perf/images:line-chart.svg",
                "0644",
            ],
            [
                "//perf/images:chrome-logo.svg",
                "0644",
            ],
            [
                "//perf/images:v8.svg",
                "0644",
            ],
            [
                "//perf/images:flutter.svg",
                "0644",
            ],
            [
                "//perf/images:webrtc.png",
                "0644",
            ],
            [
                "//perf/images:androidx.svg",
                "0644",
            ],
            [
                "//perf/images:fuchsia.svg",
                "0644",
            ],
            [
                "//perf/images:skia.svg",
                "0644",
            ],
            [
                "//perf/images:widevine.svg",
                "0644",
            ],
            [
                "//perf/images:germanium.webp",
                "0644",
            ],
            [
                "//perf/images:alpine.png",
                "0644",
            ],
            [
                "//perf/images:alpine_transparent.png",
                "0644",
            ],
        ],
        "/usr/local/share/skiaperf/dist": [
            [
                "//perf/pages:production/%s.%s" % (page, ext),
                "0644",
            ]
            for page in [
                "alerts",
                "clusters2",
                "dryrunalert",
                "help",
                "multiexplore",
                "newindex",
                "triage",
                "trybot",
                "favorites",
                "revisions",
                "regressions",
                "report",
                "playground",
            ]
            for ext in [
                "css",
                "css.map",
                "html",
                "js",
                "js.map",
            ]
        ],
        "/usr/local/share/skiaperf": [
            [
                ":gen_version",
                "0644",
            ],
        ],
    },
    entrypoint = ["/usr/local/bin/perfserver"],
    repository = "skia-public/perfserver",
)

skia_app_container(
    name = "backendserver",
    base_image = "@base-cipd//image",
    dirs = {
        "/usr/local/bin/backendserver": [
            [
                "//perf/go/backend/backendserver:backendserver",
                "0755",
            ],
        ],
        "/usr/local/share/skiaperf/configs": [
            [
                ":configs",
                "0644",
            ],
        ],
        "/usr/local/share/skiaperf/configs/spanner": [
            [
                ":configs_spanner",
                "0644",
            ],
        ],
    },
    entrypoint = "/usr/local/bin/backendserver/backendserver",
    repository = "skia-public/backendserver",
)

sh_test(
    name = "gen_version_test",
    srcs = ["gen_version_test.sh"],
    data = [":gen_version"],
)
