package(default_visibility=["//visibility:public"])

load("@build_bazel_rules_nodejs//internal/npm_install:npm_umd_bundle.bzl", "npm_umd_bundle")
load("@io_bazel_rules_sass//:defs.bzl", "sass_library")
load("//infra-sk/css_to_scss:index.bzl", "css_to_scss")
load("//infra-sk/remove_css_imports_from_js:index.bzl", "remove_css_imports_from_js")

#################################
# Sass library for elements-sk. #
#################################

ELEMENTS_SK_CSS = glob(["node_modules/elements-sk/**/*.css"])
ELEMENTS_SK_SCSS = [css.replace(".css", ".scss") for css in ELEMENTS_SK_CSS]

# The Sass compiler (and by extension the sass_binary rule) does not inline plain CSS (non-Sass)
# files imported with the @import rule[1], so we transform the elements-sk CSS stylesheets into Sass
# via the css_to_scss macro, which changes their extension and replaces `@import url(foo.css)` rules
# with Sass `@use "foo.scss"` rules.
#
# As an alternative, we could rewrite the elements-sk stylesheets in Sass and distribute both the
# *.scss and transpiled *.css files in the NPM package.
#
# [1] https://sass-lang.com/documentation/at-rules/import#plain-css-imports
[
    css_to_scss(
        name = "elements-sk_%s_generator" % css.replace("/", "__").replace(".css", ".scss"),
        src = css,
        out = css.replace(".css", ".scss")
    )
    for css in ELEMENTS_SK_CSS
]

sass_library(
    name = "elements-sk_scss",
    srcs = ELEMENTS_SK_SCSS,
)

# The UMD bundles below are only necessary for karma_mocha_test and karma_web_test BUILD targets,
# which require 3rd-party dependencies to be bundled as UMD modules and provided explicitly via the
# srcs argument.

##############################
# UMD bundles for common-sk. #
##############################

COMMON_SK_MODULES = [
    filename                                             # node_modules/common-sk/modules/dom.js
        .replace("node_modules/common-sk/modules/", "")  # dom.js
        .replace(".js", "")                              # dom
    for filename in glob(["node_modules/common-sk/modules/*.js"])
    if not filename.endswith("_test.js")
]

[
    npm_umd_bundle(
        name = "common-sk_%s_umd" % module,
        package_name = "common-sk/modules/%s" % module,
        entry_point = "@infra-sk_npm//:node_modules/common-sk/modules/%s.js" % module,
        package = "@infra-sk_npm//common-sk",
    )
    for module in COMMON_SK_MODULES
]

filegroup(
    name = "common-sk_umd",
    srcs = ["common-sk/modules/%s.umd.js" % module for module in COMMON_SK_MODULES],
)

################################
# UMD bundles for elements-sk. #
################################

ELEMENTS_SK_MODULES = [
    filename                                       # node_modules/elements-sk/icon/add-icon-sk.js
        .replace("node_modules/elements-sk/", "")  # icon/add-icon-sk.js
        .replace(".js", "")                        # icon/add-icon-sk
    for filename in glob(["node_modules/elements-sk/**/*.js"])
    if not filename.endswith("/index.js") and not filename.endswith("_test.js")
]

[
    npm_umd_bundle(
        name = "elements-sk_%s_umd" % module.replace("/", "__"),
        package_name = "elements-sk/%s" % module,
        entry_point = "@infra-sk_npm//:node_modules/elements-sk/%s.js" % module,
        package = "@infra-sk_npm//elements-sk",
        excluded = [
            # The npm_umd_bundle rule chokes when it sees CSS modules imported from JavaScript code
            # (e.g. "import './icon-sk.css'") because it treats any imports as JavaScript imports.
            # Excluding any imported CSS modules prevents said erros.
            './icon-sk.css',
            './toast-sk.css',
        ],
    )
    for module in ELEMENTS_SK_MODULES
]

# Some elements-sk modules import CSS files from JavaScript, e.g. `import "styles.css"`. When said
# elements-sk modules are bundled as UMD modules and loaded from a Karma test, the test fails
# because the `require("styles.css")` imports in the UMD modules cannot be resolved.
#
# Erasing the CSS imports from the UMD bundles prevents these errors. This is OK because the CSS
# imports are only intended for generating CSS bundles with Webpack and the css-loader plugin, and
# have no effect when Webpack is not used.
[
    remove_css_imports_from_js(
        name = "elements-sk_%s_umd_nocss" % module.replace("/", "__"),
        src = ":elements-sk/%s.umd.js" % module,
        out = ":elements-sk/%s.umd.nocss.js" % module,
    )
    for module in ELEMENTS_SK_MODULES
]

filegroup(
    name = "elements-sk_umd",
    srcs = ["elements-sk/%s.umd.nocss.js" % module for module in ELEMENTS_SK_MODULES],
)

######################
# Other UMD bundles. #
######################

npm_umd_bundle(
    name = "lit-html_umd",
    package_name = "lit-html",
    entry_point = "@infra-sk_npm//:node_modules/lit-html/lit-html.js",
    package = "@infra-sk_npm//lit-html",
)

npm_umd_bundle(
    name = "chai_umd",
    package_name = "chai",
    entry_point = "@infra-sk_npm//:node_modules/chai/lib/chai.js",
    package = "@infra-sk_npm//chai",
)

npm_umd_bundle(
    name = "sinon_umd",
    package_name = "sinon",
    entry_point = "@infra-sk_npm//:node_modules/sinon/lib/sinon.js",
    package = "@infra-sk_npm//sinon",
)
